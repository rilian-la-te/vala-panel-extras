include(CMakeDependentOption)
#Volume
pkg_check_modules(ALSA alsa IMPORTED_TARGET)
pkg_check_modules(CANBERRA libcanberra IMPORTED_TARGET)
cmake_dependent_option(ENABLE_ALSA "Enable volume applet" ON "ALSA_FOUND" OFF)
cmake_dependent_option(ENABLE_LIBCANBERRA "Volume: enable beep" ON "CANBERRA_FOUND;ALSA_FOUND" OFF)

if (ENABLE_ALSA)
    if (ENABLE_LIBCANBERRA)
        set(CANBERRA_DEFINE -D CANBERRA)
        set(CANBERRA_PACKAGE "libcanberra")
    endif()
    add_subdirectory(volume)
endif()

option(ENABLE_BATTERY "Enable battery applet" ON)
if (ENABLE_BATTERY)
    add_subdirectory(batt)
endif()

pkg_check_modules(GWEATHER gweather-3.0 IMPORTED_TARGET)
cmake_dependent_option(ENABLE_WEATHER "Enable weather applet" ON "GWEATHER_FOUND" OFF)
if (ENABLE_WEATHER)
    add_subdirectory(weather)
endif()

pkg_check_modules(XKB xkbcommon IMPORTED_TARGET)
pkg_check_modules(X11 xcb-xkb xkbcommon-x11 x11-xcb gdk-x11-3.0 IMPORTED_TARGET)
cmake_dependent_option(ENABLE_XKB "Enable xkb applet" ON "XKB_FOUND;X11_FOUND" OFF)
if (ENABLE_XKB)
    add_subdirectory(xkb)
endif()

pkg_check_modules(LIBGTOP libgtop-2.0 IMPORTED_TARGET)
cmake_dependent_option(ENABLE_LIBGTOP "Enable netload applet" ON "LIBGTOP_FOUND" OFF)
if (ENABLE_LIBGTOP)
    add_subdirectory(gtop)
endif()
